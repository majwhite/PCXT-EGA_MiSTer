//
// MiSTer PCXT RAM
// Ported by @spark2k06
//
// Based on KFPC-XT written by @kitune-san
//
`ifndef ENABLE_EMS
`define ENABLE_EMS 0
`endif

module RAM (
    input   logic           clock,
    input   logic           reset,
    input   logic           enable_sdram,
    output  logic           initilized_sdram,
    // I/O Ports
    input   logic   [19:0]  address,
    input   logic   [7:0]   internal_data_bus,
    output  logic   [7:0]   data_bus_out,
    input   logic           memory_read_n,
    input   logic           memory_write_n,
    input   logic           no_command_state,
    output  logic           memory_access_ready,
    output  logic           ram_address_select_n,
    // SDRAM
    output  logic   [12:0]  sdram_address,
    output  logic           sdram_cke,
    output  logic           sdram_cs,
    output  logic           sdram_ras,
    output  logic           sdram_cas,
    output  logic           sdram_we,
    output  logic   [1:0]   sdram_ba,
    input   logic   [15:0]  sdram_dq_in,
    output  logic   [15:0]  sdram_dq_out,
    output  logic           sdram_dq_io,
    output  logic           sdram_ldqm,
    output  logic           sdram_udqm,
     // EMS
     input   logic   [6:0]   map_ems[0:3],
     input   logic           ems_b1,
     input   logic           ems_b2,
      input   logic           ems_b3,
      input   logic           ems_b4,
      // UMB
      input   logic           umb_enabled,
      // BIOS
     input  logic    [2:0]  bios_protect_flag,
    // Wait mode
    input   logic           wait_count_clk_en,
    input   logic   [1:0]   ram_read_wait_cycle,
    input   logic   [1:0]   ram_write_wait_cycle
`ifdef PC3086_POST_TRACE
    // PC3086-only RAM-side probe. These are absent from normal revisions.
    ,output  logic   [7:0]   debug_tail_write_cpu_low
    ,output  logic   [7:0]   debug_tail_write_cpu_high
    ,output  logic   [7:0]   debug_tail_write_sdram_low
    ,output  logic   [7:0]   debug_tail_write_sdram_high
    ,output  logic   [7:0]   debug_tail_write_queue_cpu_low
    ,output  logic   [7:0]   debug_tail_write_queue_cpu_high
    ,output  logic   [7:0]   debug_tail_write_queue_sdram_low
    ,output  logic   [7:0]   debug_tail_write_queue_sdram_high
    ,output  logic   [1:0]   debug_tail_write_cpu_valid
    ,output  logic   [1:0]   debug_tail_write_sdram_valid
    ,output  logic   [1:0]   debug_tail_write_queue_cpu_valid
    ,output  logic   [1:0]   debug_tail_write_queue_sdram_valid
    ,output  logic   [3:0]   debug_tail_write_cpu_low_count
    ,output  logic   [3:0]   debug_tail_write_cpu_high_count
    ,output  logic   [3:0]   debug_tail_write_queue_cpu_low_count
    ,output  logic   [3:0]   debug_tail_write_queue_cpu_high_count
    ,output  logic   [3:0]   debug_tail_write_sdram_low_count
    ,output  logic   [3:0]   debug_tail_write_sdram_high_count
    ,output  logic   [3:0]   debug_tail_write_queue_sdram_low_count
    ,output  logic   [3:0]   debug_tail_write_queue_sdram_high_count
    // SDRAM-committed bytes at 0000:7C00, 7C01, 7DFE and 7DFF.
    ,output  logic   [31:0]  debug_boot_sector_sdram_data
    ,output  logic   [3:0]   debug_boot_sector_sdram_valid
    // First root-directory entry bytes at 0000:0500/01/08/09. The expected
    // MS-DOS 5 system image begins "IO      SYS" (49 4F ... 53 59).
    ,output  logic   [31:0]  debug_root_dir_sdram_data
    ,output  logic   [3:0]   debug_root_dir_sdram_valid
`endif
);

    typedef enum {IDLE, RAM_WRITE_1, RAM_WRITE_2, RAM_READ_1, RAM_READ_2, COMPLETE_RAM_RW, WAIT} state_t;

    state_t         state;
    state_t         next_state;
    logic   [21:0]  decoded_address;
    logic   [21:0]  latch_address;
    logic   [7:0]   latch_data;
    logic           write_command;
    logic           read_command;
    logic           prev_no_command_state;
    logic           enable_refresh;
    logic           write_protect;

    logic   [1:0]   read_wait_count;
    logic   [1:0]   write_wait_count;
    logic           access_ready;

    wire ems_bank_select = ems_b1 | ems_b2 | ems_b3 | ems_b4;
    wire ems_page_frame  = `ENABLE_EMS && (address[19:16] == 4'b1101);

    //
    // RAM Address Select (0x00000-0x9FFFF and 0xC0000-0xFFFFF).
    // A0000-BFFFF is reserved for video.
    // C4000-CFFFF is optional UMB; C0000-C3FFF remains the EGA BIOS ROM.
    // D0000-DFFFF is reserved for EMS and only responds for a mapped bank.
    //
    assign ram_address_select_n = ~(enable_sdram && ~(address[19:17] == 3'b101) &&
	                               (umb_enabled || (address[19:14] != 6'b110001 && address[19:14] != 6'b110010 && address[19:14] != 6'b110011)) &&
	                               (~ems_page_frame || ems_bank_select));
	 

    //
    // Write protect
    //
    assign write_protect = (bios_protect_flag[2] & (address[19:14] == 6'b110000))
                         | (bios_protect_flag[1] & (address[19:16] == 4'b1111))
                         | (bios_protect_flag[0] & (address[19:14] == 6'b111011));


    //
    // I/O Ports
    //
    // Address
    always_comb begin
        if (ems_b1)
            decoded_address = {1'b1, map_ems[0], address[13:0]};
        else if (ems_b2)
            decoded_address = {1'b1, map_ems[1], address[13:0]};
        else if (ems_b3)
            decoded_address = {1'b1, map_ems[2], address[13:0]};
        else if (ems_b4)
            decoded_address = {1'b1, map_ems[3], address[13:0]};
        else
            decoded_address = {2'b00, address};
    end

    // Keep the address after the bus cycle has released MEMW. KFSDRAM needs
    // the live decoded value for ACTIVE on the acceptance edge (while this
    // register still contains the previous access), then uses this copy for
    // the column command and the rest of the transaction.
    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            latch_address <= 22'd0;
        else if (state == IDLE)
            latch_address <= decoded_address;
    end

    // Data
    // Freeze the write byte once the access leaves IDLE, instead of tracking
    // the live data bus for the whole transaction. Otherwise a bus turnaround
    // that happens to land inside RAM_WRITE_1/2 (most likely at the fastest
    // CPU speed setting, where the write command pulse is only a few chipset
    // clocks wide) can commit the wrong byte to SDRAM.
    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            latch_data      <= 0;
        else if (state == IDLE)
            latch_data      <= internal_data_bus;
    end

    // Write Command
    assign write_command = ~ram_address_select_n & ~memory_write_n & ~write_protect;

    // Read Command
    assign read_command  = ~ram_address_select_n & ~memory_read_n;

    // Generate refresh timing
    always_ff @(posedge clock, posedge reset) begin
        if (reset) begin
            prev_no_command_state   <= 1'b0;
        end
        else begin
            prev_no_command_state   <= no_command_state;
        end
    end

    assign  enable_refresh  = no_command_state & ~prev_no_command_state;


    //
    // SDRAM Controller
    //
    logic   [23:0]  access_address;
    logic   [8:0]   access_num;
    logic   [15:0]  access_data_in;
    logic   [15:0]  access_data_out;
    logic           write_request;
    logic           read_request;
    logic           write_flag;
    logic           read_flag;
    logic           idle;
    logic           refresh_mode;

`ifdef PC3086_POST_TRACE
    // Observe the first-key BDA update at both sides of the RAM transaction.
    // The tail vectors represent 041Ch/d; the queue vectors represent the
    // first queue word at 041Eh/f. These probes are absent from normal cores.
    always_ff @(posedge clock, posedge reset) begin
        if (reset) begin
            debug_tail_write_cpu_low         <= 8'h00;
            debug_tail_write_cpu_high        <= 8'h00;
            debug_tail_write_sdram_low       <= 8'h00;
            debug_tail_write_sdram_high      <= 8'h00;
            debug_tail_write_queue_cpu_low   <= 8'h00;
            debug_tail_write_queue_cpu_high  <= 8'h00;
            debug_tail_write_queue_sdram_low <= 8'h00;
            debug_tail_write_queue_sdram_high <= 8'h00;
            debug_tail_write_cpu_valid       <= 2'b00;
            debug_tail_write_sdram_valid     <= 2'b00;
            debug_tail_write_queue_cpu_valid <= 2'b00;
            debug_tail_write_queue_sdram_valid <= 2'b00;
            debug_tail_write_cpu_low_count   <= 4'h0;
            debug_tail_write_cpu_high_count  <= 4'h0;
            debug_tail_write_queue_cpu_low_count <= 4'h0;
            debug_tail_write_queue_cpu_high_count <= 4'h0;
            debug_tail_write_sdram_low_count <= 4'h0;
            debug_tail_write_sdram_high_count <= 4'h0;
            debug_tail_write_queue_sdram_low_count <= 4'h0;
            debug_tail_write_queue_sdram_high_count <= 4'h0;
            debug_boot_sector_sdram_data <= 32'h00000000;
            debug_boot_sector_sdram_valid <= 4'b0000;
            debug_root_dir_sdram_data <= 32'h00000000;
            debug_root_dir_sdram_valid <= 4'b0000;
        end else begin
            // This is the RAM controller's own acceptance edge, independent
            // of the top-level trace sampler.
            if (state == IDLE && write_command) begin
                case (address)
                    20'h0041C: begin
                        debug_tail_write_cpu_low <= internal_data_bus;
                        debug_tail_write_cpu_valid[0] <= 1'b1;
                        if (!(&debug_tail_write_cpu_low_count))
                            debug_tail_write_cpu_low_count <= debug_tail_write_cpu_low_count + 1'b1;
                    end
                    20'h0041D: begin
                        debug_tail_write_cpu_high <= internal_data_bus;
                        debug_tail_write_cpu_valid[1] <= 1'b1;
                        if (!(&debug_tail_write_cpu_high_count))
                            debug_tail_write_cpu_high_count <= debug_tail_write_cpu_high_count + 1'b1;
                    end
                    20'h0041E: begin
                        debug_tail_write_queue_cpu_low <= internal_data_bus;
                        debug_tail_write_queue_cpu_valid[0] <= 1'b1;
                        if (!(&debug_tail_write_queue_cpu_low_count))
                            debug_tail_write_queue_cpu_low_count <= debug_tail_write_queue_cpu_low_count + 1'b1;
                    end
                    20'h0041F: begin
                        debug_tail_write_queue_cpu_high <= internal_data_bus;
                        debug_tail_write_queue_cpu_valid[1] <= 1'b1;
                        if (!(&debug_tail_write_queue_cpu_high_count))
                            debug_tail_write_queue_cpu_high_count <= debug_tail_write_queue_cpu_high_count + 1'b1;
                    end
                    default: ;
                endcase
            end
            // write_flag is the point at which KFSDRAM issues the write;
            // access_data_in is consequently the byte actually supplied to it.
            if (state == RAM_WRITE_1 && write_flag) begin
                case (latch_address[19:0])
                    20'h0041C: begin
                        debug_tail_write_sdram_low <= access_data_in[7:0];
                        debug_tail_write_sdram_valid[0] <= 1'b1;
                        if (!(&debug_tail_write_sdram_low_count))
                            debug_tail_write_sdram_low_count <= debug_tail_write_sdram_low_count + 1'b1;
                    end
                    20'h0041D: begin
                        debug_tail_write_sdram_high <= access_data_in[7:0];
                        debug_tail_write_sdram_valid[1] <= 1'b1;
                        if (!(&debug_tail_write_sdram_high_count))
                            debug_tail_write_sdram_high_count <= debug_tail_write_sdram_high_count + 1'b1;
                    end
                    20'h0041E: begin
                        debug_tail_write_queue_sdram_low <= access_data_in[7:0];
                        debug_tail_write_queue_sdram_valid[0] <= 1'b1;
                        if (!(&debug_tail_write_queue_sdram_low_count))
                            debug_tail_write_queue_sdram_low_count <= debug_tail_write_queue_sdram_low_count + 1'b1;
                    end
                    20'h0041F: begin
                        debug_tail_write_queue_sdram_high <= access_data_in[7:0];
                        debug_tail_write_queue_sdram_valid[1] <= 1'b1;
                        if (!(&debug_tail_write_queue_sdram_high_count))
                            debug_tail_write_queue_sdram_high_count <= debug_tail_write_queue_sdram_high_count + 1'b1;
                    end
                    default: ;
                endcase
            end
            // `access_data_in` is the controller's actual SDRAM write byte,
            // so this covers DMA transfers as well as CPU stores.
            if (state == RAM_WRITE_1 && write_flag) begin
                case (latch_address[19:0])
                    20'h07C00: begin debug_boot_sector_sdram_data[7:0]   <= access_data_in[7:0]; debug_boot_sector_sdram_valid[0] <= 1'b1; end
                    20'h07C01: begin debug_boot_sector_sdram_data[15:8]  <= access_data_in[7:0]; debug_boot_sector_sdram_valid[1] <= 1'b1; end
                    20'h07DFE: begin debug_boot_sector_sdram_data[23:16] <= access_data_in[7:0]; debug_boot_sector_sdram_valid[2] <= 1'b1; end
                    20'h07DFF: begin debug_boot_sector_sdram_data[31:24] <= access_data_in[7:0]; debug_boot_sector_sdram_valid[3] <= 1'b1; end
                    20'h00500: begin debug_root_dir_sdram_data[7:0]   <= access_data_in[7:0]; debug_root_dir_sdram_valid[0] <= 1'b1; end
                    20'h00501: begin debug_root_dir_sdram_data[15:8]  <= access_data_in[7:0]; debug_root_dir_sdram_valid[1] <= 1'b1; end
                    20'h00508: begin debug_root_dir_sdram_data[23:16] <= access_data_in[7:0]; debug_root_dir_sdram_valid[2] <= 1'b1; end
                    20'h00509: begin debug_root_dir_sdram_data[31:24] <= access_data_in[7:0]; debug_root_dir_sdram_valid[3] <= 1'b1; end
                    default: ;
                endcase
            end
        end
    end
`endif

    KFSDRAM u_KFSDRAM (
        .sdram_clock        (clock),
        .sdram_reset        (reset),
        .address            (access_address),
        .access_num         (access_num),
        .data_in            (access_data_in),
        .data_out           (access_data_out),
        .write_request      (write_request),
        .read_request       (read_request),
        .enable_refresh     (enable_refresh),
        .write_flag         (write_flag),
        .read_flag          (read_flag),
        .idle               (idle),
        .refresh_mode       (refresh_mode),
        .sdram_address      (sdram_address),
        .sdram_cke          (sdram_cke),
        .sdram_cs           (sdram_cs),
        .sdram_ras          (sdram_ras),
        .sdram_cas          (sdram_cas),
        .sdram_we           (sdram_we),
        .sdram_ba           (sdram_ba),
        .sdram_dq_in        (sdram_dq_in),
        .sdram_dq_out       (sdram_dq_out),
        .sdram_dq_io        (sdram_dq_io)
    );


    //
    // State machine
    //
    always_comb begin
        next_state = state;
        casez (state)
            IDLE: begin
                if (write_command)
                    next_state = RAM_WRITE_1;
                else if (read_command)
                    next_state = RAM_READ_1;
            end
            // Once accepted, a write owns its address and byte and must reach
            // SDRAM even if the short 25 MHz MEMW pulse has already ended.
            // Reads still abort below because their result has no recipient
            // after MEMR is released.
            RAM_WRITE_1: begin
                if (write_flag)
                    next_state = RAM_WRITE_2;
            end
            RAM_WRITE_2: begin
                if (~write_flag)
                    next_state = COMPLETE_RAM_RW;
            end
            RAM_READ_1: begin
                if (~read_command)
                    next_state = WAIT;
                if (read_flag)
                    next_state = RAM_READ_2;
            end
            RAM_READ_2: begin
                if (~read_command)
                    next_state = WAIT;
                if (~read_flag)
                    next_state = COMPLETE_RAM_RW;
            end
            COMPLETE_RAM_RW: begin
                if ((~write_command) && (~read_command))
                    next_state = IDLE;
            end
            WAIT: begin
                if (idle)
                    next_state = IDLE;
            end
        endcase
    end

    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            state <= IDLE;
        else
            state <= next_state;
    end

    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            initilized_sdram <= 1'b0;
        else if (idle)
            initilized_sdram <= 1'b1;
        else
            initilized_sdram <= initilized_sdram;
    end


    //
    // Output SDRAM Control Signals
    //
    always_comb begin
        casez (state)
            IDLE: begin
                // KFSDRAM captures row/bank on the same edge that RAM latches
                // this access. The registered address is therefore still one
                // cycle old here; use the live decode for ACTIVE only.
                access_address  = {2'b00, decoded_address};
                access_num      = 9'h001;
                access_data_in  = {8'h00, latch_data};
                write_request   = write_command ? 1'b1 : 1'b0;
                read_request    = read_command  ? 1'b1 : 1'b0;
                sdram_ldqm      = 1'b0;
                sdram_udqm      = 1'b0;
            end
            RAM_WRITE_1: begin
                access_address  = {2'b00, latch_address};
                access_num      = 9'h001;
                access_data_in  = {8'h00, latch_data};
                write_request   = 1'b1;
                read_request    = 1'b0;
                sdram_ldqm      = 1'b0;
                sdram_udqm      = 1'b0;
            end
            RAM_WRITE_2: begin
                access_address  = {2'b00, latch_address};
                access_num      = 9'h001;
                access_data_in  = {8'h00, latch_data};
                write_request   = 1'b0;
                read_request    = 1'b0;
                sdram_ldqm      = 1'b0;
                sdram_udqm      = 1'b0;
            end
            RAM_READ_1: begin
                access_address  = {2'b00, latch_address};
                access_num      = 9'h001;
                access_data_in  = 16'h0000;
                write_request   = 1'b0;
                read_request    = 1'b1;
                sdram_ldqm      = 1'b0;
                sdram_udqm      = 1'b0;
            end
            RAM_READ_2: begin
                access_address  = {2'b00, latch_address};
                access_num      = 9'h001;
                access_data_in  = 16'h0000;
                write_request   = 1'b0;
                read_request    = 1'b0;
                sdram_ldqm      = 1'b0;
                sdram_udqm      = 1'b0;
            end
            COMPLETE_RAM_RW: begin
                access_address  = 24'h000000;
                access_num      = 9'h001;
                access_data_in  = 16'h0000;
                write_request   = 1'b0;
                read_request    = 1'b0;
                sdram_ldqm      = 1'b0;
                sdram_udqm      = 1'b0;
            end
            WAIT: begin
                access_address  = 24'h000000;
                access_num      = 9'h001;
                access_data_in  = 16'h0000;
                write_request   = 1'b0;
                read_request    = 1'b0;
                sdram_ldqm      = 1'b1;
                sdram_udqm      = 1'b1;
            end
        endcase
    end


    //
    // Databus Out
    //
    logic   [7:0]   data_bus_out_reg;

    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            data_bus_out_reg    <= 0;
        else if (read_flag)
            data_bus_out_reg    <= access_data_out[7:0];
        else
            data_bus_out_reg    <= data_bus_out_reg;
    end

    assign  data_bus_out = ~read_command ? 0 : ~read_flag ? data_bus_out_reg : access_data_out[7:0];


    //
    // Ready/Wait Signal
    //
    // access_ready used to stay high through the whole access unless a
    // refresh happened to already be in progress when the command was
    // decoded. That makes RAM readiness effectively open-loop: at the
    // fastest CPU speed setting the write command pulse (~2 CPU clocks) can
    // close before the SDRAM controller has actually issued the write,
    // silently dropping it (see docs/max-speed-stability.md, RC2). Track
    // the access state machine directly instead: not ready as soon as a
    // command is decoded in IDLE, ready again only once COMPLETE_RAM_RW is
    // reached, i.e. after the SDRAM side has actually finished.
    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            access_ready <= 1'b0;
        else if (state == COMPLETE_RAM_RW)
            access_ready <= 1'b1;
        else if (state == IDLE)
            access_ready <= idle & ~(write_command | read_command);
        else
            access_ready <= 1'b0;
    end

    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            read_wait_count     <= 0;
        else if (~read_command)
            read_wait_count     <= ram_read_wait_cycle;
        else if ((wait_count_clk_en) && (read_wait_count != 0))
            read_wait_count     <= read_wait_count - 1;
        else
            read_wait_count     <= read_wait_count;
    end

    always_ff @(posedge clock, posedge reset) begin
        if (reset)
            write_wait_count    <= 0;
        else if (~write_command)
            write_wait_count    <= ram_write_wait_cycle;
        else if ((wait_count_clk_en) && (write_wait_count != 0))
            write_wait_count    <= write_wait_count - 1;
        else
            write_wait_count    <= write_wait_count;
    end

    assign  memory_access_ready = ((~ram_address_select_n) && ((~memory_read_n) || (~memory_write_n)))
                                        ? (access_ready & ((read_wait_count==0) || (~read_command)) & ((write_wait_count==0) || (~write_command))) : 1'b1;

endmodule
